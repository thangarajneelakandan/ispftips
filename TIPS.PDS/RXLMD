/* ------------------------ rexx ---------------------------- *
 | This REXX code demonstrates using the LMDLIST ISPF Service |
 | to list information about data sets under the provided     |
 | high-level-qualifier (hlq).                                |
 |                                                            |
 | If no hlq is provided then the users prefix or userid      |
 | will be used.                                              |
 |                                                            |
 | ISPF - Developers - Tips and Tricks                        |
 * ---------------------------------------------------------- */
  arg hlq
/* -------------------------------- *
 | initialize our working variables |
 * -------------------------------- */
  parse value '' with null dsn
  parse value '0 0 0' with total count used

/* --------------------------- *
 | Validate and/or set the HLQ |
 * --------------------------- */
  if hlq = null then do
    if sysvar('syspref') /= null then
    hlq = sysvar('syspref')
    else hlq = sysvar('sysuid')
  end

/* ----------------------- *
 | Define ISPF Environment |
 * ----------------------- */
  Address ISPEXEC
/* ------------- *
 | Setup LMDINIT |
 * ------------- */
  "Lmdinit Listid(LMD) Level("hlq")"
/* ----------------------------------- *
 | Process ALL data sets under the HLQ |
 * ----------------------------------- */
  do forever
    "Lmdlist Listid("lmd") Stats(YES) Dataset(dsn) Option(LIST)"
/* ---------------------------- *
 | If return > 0 then finish up |
 * ---------------------------- */
    if rc > 0 then do
      "Lmdfree listid("lmd")"
      say "All done - count:" count 'used:' used
      exit 4
    end
/* ----------------------------- *
 | Update counter and report out |
 * ----------------------------- */
    count = count + 1
    say count":" "Dataset:" dsn "Vol:" zdlvol "Used:" zdlsize ,
      "Device: *"zdldev"*" ,
      "Mig:" zdlmigr
    sysvolume = ""
    if datatype(zdlsize) = 'NUM' then
    used = used + zdlsize
  end
