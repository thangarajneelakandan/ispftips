/* ------------------- REXX-------------------------------- *
 * This routine will demonstrate the use of LMMLIST         *
 *                                                          *
 * Syntax:  %rxlmm dsname member                            *
 *                                                          *
 * where member is optional and may be a prefix (e.g. xxx*) *
 *                                                          *
 * ISPF - Developers - Tips and Tricks                      *
 * -------------------------------------------------------- */
 arg dsn imem
 if dsn = "" then exit 8

/* ------------------ *
 * Define ISPF Dataid *
 * ------------------ */
 Address ISPEXEC
 "LMINIT DATAID(STATUS) DATASET("dsn")"
 "LMOPEN DATAID("STATUS") OPTION(INPUT)"
/* ------------ *
 * Set defaults *
 * ------------ */
 parse value "" with member mem. ,
         ZLCDATE ZLMDATE ZLVERS ZLMOD ZLMTIME ZLCNORC,
         ZLINORC ZLMNORC ZLUSER ,
         zlcnorce zlinorce zlmnorce
 mem.0  = 0

/* ----------------------- *
 * Now process all members *
 * ----------------------- */
 do forever
    if length(imem) = 0 then
      "LMMLIST Dataid("status") OPTION(LIST) MEMBER(MEMBER)" ,
              "STATS(YES)"
    else
      "LmmList Dataid("status") Option(List) Member(member)",
              "Pattern("imem") stats(yes)"
    if rc = 4 then do
       say 'no members found matching pattern'
       exit
       end
   /* --------------------------------- *
    * If RC 8 or more leave the do loop *
    * --------------------------------- */
    if rc > 7 then leave
   /* ------------------------------ *
    * Add each member info to a stem *
    * ------------------------------ */
    c = mem.0 + 1
    mem.c = member ,
        ZLCDATE  ZLMDATE  ZLVERS ZLMOD ZLMTIME ZLCNORC ,
        ZLINORC ZLMNORC ZLUSER ,
        zlcnorce zlinorce zlmnorce
    mem.0 = c
    end

/* ------------------------- *
 * Close and Free the Dataid *
 * ------------------------- */
 "LMClose Dataid("status")"
 "LMFree  Dataid("status")"
/* ------------------- *
 * Say the Header line *
 * ------------------- */
  say "Member  ",
         "Cdate    MDate   Ver Mod Time   Cur  Init   Mod User"
/* ------------------------------ *
 * Now say the Member information *
 * ------------------------------ */
 do i = 1 to mem.0
    say mem.i
    end
