/* --------------------  rexx procedure  -------------------- *
 | Name:      RXPOPM                                          |
 | ISPF - Developers - Tips and Tricks                        |
 |                                                            |
 | Options:   ISPF variable poplog - if x then save info      |
 |            in the poplog variable and vput it.             |
 |            Otherwise put info into the member as a         |
 |            ISPF Edit noteline.                             |
 |                                                            |
 | Function:  ISPF Edit Macro to propose ADDPOP ROW/COLUMN    |
 |            values for an ISPF POPUP Panel                  |
 * ---------------------------------------------------------- */
 Address ISREdit
 'Macro'

/* ------------------------------------------- *
 | Check for the WINDOW( keyword in the member |
 * ------------------------------------------- */
  "find 'WINDOW(' first"
  if rc > 0 then do
    'end'
    exit
  end

/* --------------------------------- *
 | Verify it is on a )BODY statement |
 | - if not exit Edit                |
 * --------------------------------- */
  '(data) = line .zcsr'
  if left(translate(data),5) /= ')BODY' then do
     'end'
     exit
     end

/* -------------------------------------------- *
 | Get the member name and then the window info |
 * -------------------------------------------- */
  '(member) = member'
  parse value translate(data) with . 'WINDOW('col','row')' .

/* -------------------------- *
 | VGET the processing option |
 * -------------------------- */
  Address ISPExec 'vget (poplog)'

/* ----------------------------------- *
 | Calculate the ideal AddPop location |
 * ----------------------------------- */
  col = ((80 - col) % 2 ) +1
  row = ((23 - row) % 2 ) +1
  data = row  col

/* ------------------------------------------------- *
 | If poplog is x then put the ideal window location |
 | back in the poplog variable and vput it.          |
 |                                                   |
 | Otherwise insert as a noteline at the top of the  |
 | member.                                           |
 * ------------------------------------------------- */
  if poplog = 'x'
  then do
    poplog = data
    Address ISPExec 'vput (poplog)'
  'end'
  end
  else do
    'reset'
    'reset find'
    Data = 'Suggested ADDPOP Command: ADDPOP Row('row') Column('col')'
    'line_before 1 = noteline (data)'
    'cursor = 1 1'
  end
