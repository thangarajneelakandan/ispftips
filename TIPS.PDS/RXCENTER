/* ---------------------- REXX ------------------------ *
 | Demonstration of processing records in a range.      |
 |                                                      |
 | ISPF - Developers - Tips and Tricks                  |
 |                                                      |
 | Center selected records.                             |
 |                                                      |
 | Record selection:                                    |
 |    C   -  individual record                          |
 |    C## -  ## records starting at the selected record |
 |    CC  -  paired for start and ending records        |
 |                                                      |
 | If no records selected then all are processed.       |
 |                                                      |
 | Can pass a data width to be used for the centering   |
 | when invoking this edit macro.                       |
 * ---------------------------------------------------- */
  Address ISREdit
/* ----------------------------------------------------- *
 | Indicate this is a macro and accept passed parms into |
 | variable macopt while NOT Processing any row cmds     |
 * ----------------------------------------------------- */
  'Macro (dw) NOPROCESS'
/* ------------------------------- *
 | Request Edit Identify any Range |
 * ------------------------------- */
  "Process Range C"
/* -------------------------------------------------------- *
 | Get the start (zfrange) and stop (zlrange) row locations |
 * -------------------------------------------------------- */
  "(start) = linenum .zfrange"
  "(stop) = linenum .zlrange"
/* ---------------------------------------------- *
 | Test if a parm indicating the width to use for |
 | centering the text was passed. If not use the  |
 | actual data width.                             |
 * ---------------------------------------------- */
  if dw = '' then
  '(dw) = data_width'
/* ----------------------------------------------- *
 | Loop through all the records from start to stop |
 | centering the data on each record and then      |
 | replacing the data on that row.                 |
 * ----------------------------------------------- */
  do i = start to stop
    '(data) = line' i
    data = center(strip(data),dw)
    'line' i '= (data)'
  end
